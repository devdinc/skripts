import:
    ch.njol.skript.log.SkriptLogger
    ch.njol.skript.lang.Variable
    ch.njol.skript.Skript

effect set default scope for scoped variables in current script to (a:current (x:folder|x:package|y:script)|b:(folder|package) %-string%|c: %-script%):
    parse:
        set {_origin} to SkriptLogger.getNode()
        set {_realscript} to script ({_origin}.getConfig().getFileName())
        continue
    trigger:
        if parse tags contains "a":
            if parse tags contains "x":
                set {_scriptsFolder} to Skript.getInstance().getScriptsFolder().toPath().toAbsolutePath()
                set {_scriptParent} to {_origin}.getConfig().getPath().getParent().toAbsolutePath()
                set {_relative} to {_scriptsFolder}.relativize({_scriptParent})
                set {_currentscript} to {_relative}.toString().replace("\\", "/")
                set {-scoped.sk::default_scope::%{_realscript}%} to {_currentscript}
            else if parse tags contains "y":
                set {-scoped.sk::default_scope::%{_realscript}%} to {_realscript}
        else if parse tags contains "b":
            set {-scoped.sk::default_scope::%{_realscript}%} to expr-1
        else if parse tags contains "c":
            set {-scoped.sk::default_scope::%{_realscript}%} to expr-2
            
local function scoped_ctx(origin: object, event: object, var: object, script: object, mark: number) :: strings:
    # origin + script
    set {_realscript} to script ({_origin}.getConfig().getFileName())

    # --- scope ---
    if {_mark} is 0:
        set {_scope} to {_realscript} if {-scoped.sk::default_scope::%{_realscript}%} is not set else {-scoped.sk::default_scope::%{_realscript}%}
    else if {_mark} is 1:
        set {_scope} to {_script}
    else if {_mark} is 2:
        set {_scriptsFolder} to Skript.getInstance().getScriptsFolder().toPath().toAbsolutePath()
        set {_scriptParent} to {_origin}.getConfig().getPath().getParent().toAbsolutePath()
        set {_relative} to {_scriptsFolder}.relativize({_scriptParent})
        set {_scope} to {_relative}.toString().replace("\\", "/")

    set {_prefix} to ""
    set {_suffix} to ""
    if {_var}.isLocal() is true:
        set {_prefix} to "_"
        
    if {_var}.isEphemeral() is true:
        set {_prefix} to "-"
        
    if {_var}.isList() is true:
        set {_suffix} to "::*"

    # --- name ---
    set {_name} to {_var}.getName().toUnformattedString({_event})
    replace "::*" with "" in {_name}
    
    set {_ctx::1prefix} to {_prefix}
    set {_ctx::2var} to "%{_scope}%::%{_name}%%{_suffix}%"
    return {_ctx::*}


expression scoped [variable] %$objects% [([with]in|from|by) (1:%-script%|2:current (folder|package))]:
    parse:
        expr-1 is an instance of Variable
        set {_var} to expr-1
        
        # we use this to capture backing expression's script, could be made cleaner.
        # we use origin.toString 
        set {_origin} to SkriptLogger.getNode()
        continue

    get:
        set {_ctx::*} to scoped_ctx({_origin}, event, {_var}, expr-2, parse mark)
        # todo others
        # we do this to remove the warning message
        if {_ctx::1} is "_":
            return variable "_scoped::%{_ctx::2}%" with custom-event event
        else if {_ctx::1} is "-":
            return variable "-scoped::%{_ctx::2}%" with custom-event event
        else:
            return variable "scoped::%{_ctx::2}%" with custom-event event

    set:
        set {_ctx::*} to scoped_ctx({_origin}, event, {_var}, expr-2, parse mark)
        
        if {_ctx::1} is "_":
            set variable "_scoped::%{_ctx::2}%" with custom-event event to change values
        else if {_ctx::1} is "-":
            set variable "-scoped::%{_ctx::2}%" with custom-event event to change values
        else:
            set variable "scoped::%{_ctx::2}%" with custom-event event to change values

    add:
        set {_ctx::*} to scoped_ctx({_origin}, event, {_var}, expr-2, parse mark)

        if {_ctx::1} is "_":
            add change values to variable "_scoped::%{_ctx::2}%" with custom-event event
        else if {_ctx::1} is "-":
            add change values to variable "-scoped::%{_ctx::2}%" with custom-event event
        else:
            add change values to variable "scoped::%{_ctx::2}%" with custom-event event

    remove:
        set {_ctx::*} to scoped_ctx({_origin}, event, {_var}, expr-2, parse mark)

        if {_ctx::1} is "_":
            remove change values from variable "_scoped::%{_ctx::2}%" with custom-event event
        else if {_ctx::1} is "-":
            remove change values from variable "-scoped::%{_ctx::2}%" with custom-event event
        else:
            remove change values from variable "scoped::%{_ctx::2}%" with custom-event event

    remove all:
        set {_ctx::*} to scoped_ctx({_origin}, event, {_var}, expr-2, parse mark)

        if {_ctx::1} is "_":
            remove all change values from variable "_scoped::%{_ctx::2}%" with custom-event event
        else if {_ctx::1} is "-":
            remove all change values from variable "-scoped::%{_ctx::2}%" with custom-event event
        else:
            remove all change values from variable "scoped::%{_ctx::2}%" with custom-event event

    delete:
        set {_ctx::*} to scoped_ctx({_origin}, event, {_var}, expr-2, parse mark)

        if {_ctx::1} is "_":
            delete variable "_scoped::%{_ctx::2}%" with custom-event event
        else if {_ctx::1} is "-":
            delete variable "-scoped::%{_ctx::2}%" with custom-event event
        else:
            delete variable "scoped::%{_ctx::2}%" with custom-event event

    reset:
        set {_ctx::*} to scoped_ctx({_origin}, event, {_var}, expr-2, parse mark)

        if {_ctx::1} is "_":
            delete variable "_scoped::%{_ctx::2}%" with custom-event event
        else if {_ctx::1} is "-":
            delete variable "-scoped::%{_ctx::2}%" with custom-event event
        else:
            delete variable "scoped::%{_ctx::2}%" with custom-event event

