import:
	org.bukkit.NamespacedKey
	org.bukkit.persistence.PersistentDataHolder
	org.bukkit.persistence.PersistentDataType
	java.io.ByteArrayInputStream
	java.io.ByteArrayOutputStream
	org.bukkit.util.io.BukkitObjectInputStream
	org.bukkit.util.io.BukkitObjectOutputStream
	java.util.Base64
# Sadly we use base64 as primitive types such as byte array does not exist in skript.

expression p[ersistent ]d[ata ]c[ontainer] of %object% (1:|2:with type %-object%) for [key ]%string%:
	parse:
		continue

	get:
		set {_holder} to expr-1
		set {_key} to expr-3
		set {_ns} to NamespacedKey.fromString({_key}, null)
		if {_ns} is null:
			return {_none}

		set {_pdc} to {_holder}.getPersistentDataContainer()

		if parse mark is 1:
			if {_pdc}.has({_ns}, PersistentDataType.STRING) is false:
				return {_none}

			set {_encoded} to {_pdc}.get({_ns}, PersistentDataType.STRING)
			set {_bytes} to Base64.getDecoder().decode({_encoded})
			set {_in} to new BukkitObjectInputStream(new ByteArrayInputStream({_bytes}))
			set {_obj} to {_in}.readObject()
			{_in}.close()
			return {_obj}

		else:
			return {_pdc}.get({_ns}, expr-2)

	set:
		set {_holder} to expr-1
		set {_key} to expr-3
		set {_value} to change value
		set {_ns} to NamespacedKey.fromString({_key}, null)
		if {_ns} is null:
			stop

		set {_pdc} to {_holder}.getPersistentDataContainer()

		if {_value} is null:
			{_pdc}.remove({_ns})
			stop

		if parse mark is 1:
			set {_baos} to new ByteArrayOutputStream()
			set {_out} to new BukkitObjectOutputStream({_baos})
			{_out}.writeObject({_value})
			{_out}.close()

			set {_encoded} to Base64.getEncoder().encodeToString({_baos}.toByteArray())
			{_pdc}.set({_ns}, PersistentDataType.STRING, {_encoded})

		else:
			{_pdc}.set({_ns}, expr-2, {_value})

	delete:
		set {_holder} to expr-1
		set {_key} to expr-3
		set {_ns} to NamespacedKey.fromString({_key}, null)
		if {_ns} is null:
			stop

		set {_pdc} to {_holder}.getPersistentDataContainer()
		{_pdc}.remove({_ns})

expression pdt str[ing]:
    parse:
        continue
    get:
        return PersistentDataType.STRING


expression pdt int[eger]:
    parse:
        continue
    get:
        return PersistentDataType.INTEGER


expression pdt long:
    parse:
        continue
    get:
        return PersistentDataType.LONG


expression pdt float:
    parse:
        continue
    get:
        return PersistentDataType.FLOAT


expression pdt double:
    parse:
        continue
    get:
        return PersistentDataType.DOUBLE


expression pdt byte:
    parse:
        continue
    get:
        return PersistentDataType.BYTE


expression pdt short:
    parse:
        continue
    get:
        return PersistentDataType.SHORT


expression pdt bool[ean]:
    parse:
        continue
    get:
        return PersistentDataType.BOOLEAN

