local effect testFail %string%[, %-string%]:
    trigger:
        set {_msg} to " %expr-2%" if expr-2 is set else ""
        send "[<gold>Skript<reset>] [&cTEST FAILURE<reset>] %expr-1%%{_msg}%" to console

event "skriptTest":
    pattern: test %string%
    event-values: string, boolean
    check:
        set {-test.sk::tests::%current script%::%expr-1%} to "%expr-1%"
        if "%current script%/%expr-1%" is event-string:
            continue
            
on load:
    delete {-test.sk::*}

expression all tests [with test name %-string%] [([with]in|from) %-script%]:
    parse:
        set {_scoped} to false if expr-2 is not set
        continue

    get:
        if {_scoped} is false:
            loop indices of {-test.sk::tests::*}:
                loop {-test.sk::tests::%loop-value%::*}:
                    if any:
                        expr-1 is not set
                        loop-value-2 is expr-1
                    then:
                        add "%loop-value-1%/%loop-value-2%" to {_r::*}

        else:
            if expr-1 is not set:
                loop {-test.sk::tests::%expr-2%::*}:
                    add "%expr-2%/%loop-value%" to {_r::*}
            else if {-test.sk::tests::%expr-2%::%expr-1%} is set:
                add "%expr-2%/%expr-1%" to {_r::*}

        return {_r::*}
        
effect (1:|2:auto)run [test][s] %strings%:
    trigger:
        set {_tests::*} to expr-1
        set {_alltests::*} to all tests
        loop {_tests::*}:
            delete {-test.sk::errors::%loop-value%} 
            set {_list::string} to loop-value
            set {_list::boolean} to true if parse mark is 2 else false
            call custom event "skriptTest" with {_list::*}
            if any:
                loop-value contains "f39f0f4a-31ee-4b71-87e9-38ddba3a2313"
                {_alltests::*} does not contain loop-value
            then:
                add 1 to {_forgottenTestResults}
                continue loop
            add 1 to {_testFails} if {-test.sk::errors::%loop-value%} is greater than 0
        set {_validTestAmount} to size of {_tests::*} - {_forgottenTestResults}
        if {_validTestAmount} is not 0:
            send "[<gold>Skript<reset>] <yellow>%{_validTestAmount} - {_testFails}%/%{_validTestAmount}% tests passed." to console
        else if size of {_alltests::*} is greater than 0: # skipping if there is no initial hidden test 
            send "[<gold>Skript<reset>] <yellow>No tests found." to console

on load:
    wait 1 tick
    run test "f39f0f4a-31ee-4b71-87e9-38ddba3a2313"
    wait 1 tick
    set {_tests::*} to all tests
    autorun {_tests::*}

import:
    ch.njol.skript.lang.Condition

effect (3:|4:(no|without) (halt[ing]|fail[(-| )]safe|abort[ing])) assert(1:|1: true|2: false)[ with (6:|5:no )[[error] message][ %-string%]]\: <(.+)>:
    usable in:
        custom event "skriptTest"
    trigger:
        set {_raw} to first element of regex-1
        set {_cond} to Condition.parse({_raw}, "Can't understand condition: " + {_raw})
        set {_ok} to {_cond}.check(event)
        set {_label} to "assert true" if parse tags contains "1" else "assert false"
        set {_test} to event.getEventValue("string")
        if all:
            parse tags contains "1"
            {_ok} is false
        then:
            set {_bool1} to true
            
        if all:
            parse tags contains "2"
            {_ok} is true
        then:
            set {_bool2} to true
            
        if any:
            {_bool1} is true
            {_bool2} is true
        then:
            if parse tags contains "6":
                testFail "Test ""%{_test}%"" with condition ""%{_label}%: %{_raw}%"" failed", expr-1
            add 1 to {-test.sk::errors::%{_test}%}
            if parse tags contains "3":
                delay effect

effect (3:|4:(no|without) (halt[ing]|fail[(-| )]safe|abort[ing])) fail test[ with (6:|5:no )[[error] message][ %-string%]]:
    usable in: 
        custom event "skriptTest"
    trigger:
        set {_test} to event.getEventValue("string")
        if parse tags contains "6":
            testFail "Test ""%{_test}%"" failed", expr-1
        add 1 to {-test.sk::errors::%{_test}%}
        if parse tags contains "3":
            delay effect
        
effect stop auto [test] execution [here]:
    usable in:
        custom event "skriptTest"
    trigger:
        set {_bool} to event.getEventValue("boolean")
        if {_bool} is true:
            delay effect
        
test "f39f0f4a-31ee-4b71-87e9-38ddba3a2313 hidden test":
    set {_test} to event-string # contains folder so we are fine
    without halting assert true with no error message: {_none} is set 
    without halting assert true with no error message: {_none} is set 
    without halting assert true with no error message: {-test.sk::errors::%{_test}%} is 2
    broadcast "<red>Something very wrong occurred with test framework(error no 1)" if {-test.sk::errors::%{_test}%} is 3
    assert true with no error message: {_none} is set 
    broadcast "<red>Something very wrong occurred with test framework(error no 2)"
