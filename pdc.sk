# =============================================================================
# Persistent Data Container (PDC) Utility Expressions for Skript
# =============================================================================
# WARNING: READ MIGRATION NOTES
#
# -----------------------------------------------------------------------------
# Overview
# -----------------------------------------------------------------------------
#
# This file provides a set of Skript expressions and helpers for interacting
# with Bukkit's PersistentDataContainer (PDC).
#
# It supports:
# - Native PersistentDataType (PDT) values
# - Arbitrary object storage via Java serialization into BYTE_ARRAY
#
#
# -----------------------------------------------------------------------------
# Migration Notice (Base64 REMOVED)
# -----------------------------------------------------------------------------
#
# Previous versions serialized arbitrary objects using Base64 encoding and
# stored them as STRING values.
#
# This implementation NO LONGER uses Base64.
#
# Arbitrary objects are now:
# - Serialized directly using BukkitObjectOutputStream
# - Stored natively as PersistentDataType.BYTE_ARRAY
#
# This is a storage-format change ONLY.
# Arbitrary object support is still fully supported.
#
#
# REQUIRED USER ACTION
# --------------------
#
# - Existing Base64-encoded PDC values are NOT compatible.
# - Stored data must be migrated or deleted.
#
# Scripts do NOT need syntax changes, but stored values written by older
# versions will not deserialize correctly.
#
# =============================================================================
# REQUIREMENTS
# =============================================================================
#
# - Paper server
# - Skript
# - skript-reflect
# - skBee
#
# -----------------------------------------------------------------------------
# Key Features
# -----------------------------------------------------------------------------
#
# 1. Unified "key in pdc" expression
#    - Read, write, and delete PDC entries using a single expression.
#    - Automatically resolves:
#        PersistentDataHolder → PersistentDataContainer
#
# 2. Native + arbitrary object storage
#    - When a PersistentDataType is provided:
#        → Values are stored natively.
#    - When omitted:
#        → Values are serialized and stored as BYTE_ARRAY.
#
# 3. NamespacedKey convenience expression
#    - Allows creation of NamespacedKey from strings like "plugin:key".
#
# 4. Safety and validation
#    - Runtime validation ensures correct types for key, container, and PDT.
#    - Invalid expressions fail silently to avoid hard Skript errors.
#
#
# -----------------------------------------------------------------------------
# Serialization Notes
# -----------------------------------------------------------------------------
#
# - Arbitrary objects are serialized using BukkitObjectOutputStream.
# - Data is stored directly as byte[] via PersistentDataType.BYTE_ARRAY.
# - Objects MUST implement java.io.Serializable.
#
# Compared to Base64:
# - Lower overhead
# - No string encoding/decoding
# - Direct compatibility with Bukkit PDC APIs
#
#
# -----------------------------------------------------------------------------
# Syntax Summary
# -----------------------------------------------------------------------------
#
# Reading:
#   set {_value} to key "plugin:test" within player's pdc
#   set {_value} to key "plugin:test" in player's pdc for pdt string
#
# Writing:
#   set key "plugin:test" within player's pdc to {_object}
#   set key "plugin:test" in player's pdc for pdt integer to 5
#
# Deleting:
#   delete key "plugin:test" within player's pdc
#
#
# -----------------------------------------------------------------------------
# Notes
# -----------------------------------------------------------------------------
#
# - If no PDT is specified, BYTE_ARRAY storage is used.
# - Deleting or setting a value to null removes the key.
#
#
# -----------------------------------------------------------------------------
# Caveats
# -----------------------------------------------------------------------------
#
# - Deserialization failures will propagate runtime errors.
# - Class changes may break previously serialized data.
# - Old Base64-encoded values must be migrated manually.
#
# =============================================================================

import:
    org.bukkit.NamespacedKey
    org.bukkit.persistence.PersistentDataContainer
    org.bukkit.persistence.PersistentDataHolder
    org.bukkit.persistence.PersistentDataType
    java.io.ByteArrayInputStream
    java.io.ByteArrayOutputStream
    org.bukkit.util.io.BukkitObjectInputStream
    org.bukkit.util.io.BukkitObjectOutputStream
    ch.njol.skript.lang.Variable

local function validate_pdcexpr(key: object, pdc: object, pdt: object) :: boolean:
    if all:
        {_pdt} is set
        {_pdt} is not an instance of PersistentDataType
    then:
        return false
    if any:
        {_key} is not an instance of NamespacedKey
        {_pdc} is not an instance of PersistentDataContainer
    then:
        return false
    return true

# e.g. key "plugin:test" in player's pdc for type byte 
expression [devdinc] %object% [with]in %object%[ for %-object%]: 
    parse:
        set {_key} to expr-1
        set {_pdt} to expr-3
            
        if any:
            "%{_key}%" is "[devdinc] [namespaced]( |-)key %%string%%"
            {_key} is instance of Variable
        then:
            if any:
                "%{_pdt}%" contains "[devdinc] (pdt|[persistent data ]type)"
                "%{_pdt}%" contains "PersistentDataType"
                {_pdt} is instance of Variable
                {_pdt} is not set
            then:
                continue
        stop

    get:
        set {_key} to expr-1
        set {_pdc} to expr-2.getPersistentDataContainer() if expr-2 is instance of PersistentDataHolder else expr-2
        set {_pdt} to expr-3
        
        stop if validate_pdcexpr({_key}, {_pdc}, {_pdt}) is false
            
        if {_pdt} is not set:
            if {_pdc}.has({_key}, PersistentDataType.BYTE_ARRAY) is false:
                return {_none}

            set {_bytes} to {_pdc}.get({_key}, PersistentDataType.BYTE_ARRAY)
            set {_in} to new BukkitObjectInputStream(new ByteArrayInputStream({_bytes}))
            set {_obj} to {_in}.readObject()
            {_in}.close()
            return {_obj}
        else:
            return {_pdc}.get({_key}, {_pdt})

    set:
        set {_key} to expr-1
        set {_pdc} to expr-2.getPersistentDataContainer() if expr-2 is instance of PersistentDataHolder else expr-2
        set {_pdt} to expr-3
        
        stop if validate_pdcexpr({_key}, {_pdc}, {_pdt}) is false
        
        set {_value} to change value
        
        if any:
            {_value} is null
            {_value} is not set
        then:
            {_pdc}.remove({_key})
            stop

        if {_pdt} is not set:
            set {_baos} to new ByteArrayOutputStream()
            set {_out} to new BukkitObjectOutputStream({_baos})
            {_out}.writeObject({_value})
            {_out}.close()
            
            set {_boxedBytes::*} to ...{_baos}.toByteArray()
            set {_bytes} to new byte[size of {_boxedBytes::*}]
            loop {_boxedBytes::*}:
                set {_bytes}[loop-index parsed as number - 1] to loop-value
                
            {_pdc}.set({_key}, PersistentDataType.BYTE_ARRAY, {_bytes})

        else:
            {_pdc}.set({_key}, {_pdt}, {_value})

    delete:
        set {_key} to expr-1
        set {_pdc} to expr-2.getPersistentDataContainer() if expr-2 is instance of PersistentDataHolder else expr-2
        set {_pdt} to expr-3
        
        stop if validate_pdcexpr({_key}, {_pdc}, {_pdt}) is false
        
        {_pdc}.remove({_key})

# e.g
# Creates a NamespacedKey from a string like "plugin:key"
expression [devdinc] [namespaced]( |-)key %string%:
    parse:
        continue
    get:
        return NamespacedKey.fromString(expr-1, null)
        
object property p[ersistent ]d[ata ]c[ontainer]:
    parse:
        continue
    get:
        return expr-1.getPersistentDataContainer()

expression [devdinc] (pdt|[persistent data ]type) (1:str[ing]|2:int[eger]|3:long|4:float|5:double|6:byte|7:short|8:bool[ean]):
    parse:
        continue
    get:
        return switch return parse mark:
            case 1 -> PersistentDataType.STRING
            case 2 -> PersistentDataType.INTEGER
            case 3 -> PersistentDataType.LONG
            case 4 -> PersistentDataType.FLOAT
            case 5 -> PersistentDataType.DOUBLE
            case 6 -> PersistentDataType.BYTE
            case 7 -> PersistentDataType.SHORT
            case 8 -> PersistentDataType.BOOLEAN
            default -> {_none}
