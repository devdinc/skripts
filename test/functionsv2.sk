import:
    java.util.HashMap

test "supplier and getter":
    set {_n} to (lambda:+ 1).get()
    assert true with error message "supplier fail": {_n} is 1
    assert true with error message "getter fail": (getter: "ok").get() is "ok"
    assert true with error message "supplier basic failed": (supplier: "test").get() is "test"
    assert true with error message "supplier expression failed": (supplier: 2 + 3).get() is 5


test "runnable":

    clear {-functions::test::*}
    (lambda:- set {-functions::test::temp} to 1).run()
    assert true with error message "runnable fail": {-functions::test::temp} is 1

    (lambda:- set {-functions::test::ran} to true).run()
    assert true with error message "runnable side-effect failed": {-functions::test::ran} is true


test "consumer and accepter":

    clear {-functions::test::*}
    (consumer {_x}: set {-functions::test::c} to {_x}).accept(5)
    assert true with error message "consumer fail": {-functions::test::c} is 5

    (accepter {_x}: set {-functions::test::a} to {_x}).accept("ok")
    assert true with error message "accepter fail": {-functions::test::a} is "ok"


test "function and applier":

    assert true with error message "function fail": (function {_x}: {_x} * 2).apply(3) is 6
    assert true with error message "applier fail": (applier {_x}: "%{_x}%!").apply("hi") is "hi!"
    assert true with error message "function basic failed": (function {_x}: {_x} * 5).apply(5) is 25
    assert true with error message "function string failed": (function {_x}: "%{_x}%_ok").apply("abc") is "abc_ok"
    assert true with error message "mixed usage failed": (function {_x}: {_x} + 1).apply(9) is 10


test "biconsumer and biaccepter":

    clear {-functions::test::*}
    (biconsumer {_a}, {_b}: set {-functions::test::bc} to "%{_a}%,%{_b}%").accept(1, 2)
    assert true with error message "biconsumer fail": {-functions::test::bc} is "1,2"

    (biaccepter {_a}, {_b}: set {-functions::test::ba} to "%{_a}%-%{_b}%").accept("x", "y")
    assert true with error message "biaccepter fail": {-functions::test::ba} is "x-y"


test "bifunction and biapplier":

    assert true with error message "bifunction fail": (bifunction {_a}, {_b}: {_a} + {_b}).apply(2, 3) is 5
    assert true with error message "biapplier fail": (biapplier {_a}, {_b}: "%{_a}%:%{_b}%").apply("a", "b") is "a:b"
    assert true with error message "bifunction failed": (bifunction {_a}, {_b}: {_a} + {_b}).apply(3, 4) is 7


test "consumer repeated invocation":

    clear {-functions::test::*}
    set {-functions::test::sum} to 0
    set {_adder} to (consumer {_x}: add {_x} to {-functions::test::sum})
    {_adder}.accept(1)
    {_adder}.accept(2)
    {_adder}.accept(3)
    assert true with error message "consumer repeat failed": {-functions::test::sum} is 6


test "biconsumer variable list":

    clear {-functions::test::*}
    (biconsumer {_a}, {_b}: set {-functions::test::pair::*} to {_a} and {_b}).accept("x", "y")
    assert true with error message "biconsumer failed": {-functions::test::pair::1} is "x"
    assert true with error message "biconsumer failed": {-functions::test::pair::2} is "y"


test "java stream variable list":

    clear {-functions::test::*}
    set {-functions::test::varl} to 0
    set {_map} to new HashMap()
    {_map}.put(1, 1)
    {_map}.put(2, 3)
    {_map}.put(3, 2)
    (lambda {_arg}:- {_arg}.values().stream().forEach(lambda {_x}:- add {_x} to {-functions::test::varl})).accept({_map})
    assert true with error message "varlist failed": {-functions::test::varl} is 6


test "array handling":
    assert true with error message "array failed": (lambda {_varl}:+ sum(...{_varl})).apply([1, 2, 3]) is 6


test "real variable list rejection":

    set {_test::*} to 1, 2 and 3
    assert false with error message "unexpected: real variable list succeeded": (function {_varl::*}: sum({_varl::*})).apply(raw expression of {_test::*}) is 6


test "run lambda helpers":
    set {_sup} to lambda:+ "yes"
    assert true with error message "run supplier fail": (run lambda {_sup}) is "yes"
    assert true with error message "run function fail": (run lambda (lambda {_x}:+ {_x}) with "test") is "test"
    
test "java call":
    set {_lam} to lambda:+ new HashMap()
    set {_val} to {_lam}.get()
    assert true with error message: {_val}.getClass().getSimpleName().equalsIgnoreCase("hashmap") is true
    

